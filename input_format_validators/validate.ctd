# This is an input validator for building inspection problem, written for the 
# checktestdata validation language (see:
# https://github.com/DOMjudge/checktestdata/blob/master/doc/format-spec.md).

# First line: number of buildings
INT(1,999,num_buildings) NEWLINE

# Second line: sequence of building IDs to inspect
INT(1,999,num_to_inspect) SPACE
REPI(i,num_to_inspect-1)
    INT(1,999,inspect_id[i]) SPACE
END
INT(1,999,inspect_id[num_to_inspect-1]) NEWLINE

# Following lines: one for each building
REPI(j,num_buildings)
    INT(1,999,building_id[j]) SPACE
    INT(0,998,num_connections[j]) SPACE
    REPI(k,num_connections[j]-1)
        INT(1,999,conn[j*999+k]) SPACE
    END
    IF(num_connections[j] > 0)
        INT(1,999,conn[j*999+num_connections[j]-1]) NEWLINE
    ELSE
        NEWLINE
    END
END

# verify uniqueness among all building IDs
ASSERT(UNIQUE(building_id))

# verify uniqueness among buildings to inspect
ASSERT(UNIQUE(inspect_id))

# verify uniqueness of connections for each building
REPI(j,num_buildings)
    REPI(k,num_connections[j])
        REPI(m,k)
            ASSERT(conn[j*999+k] != conn[j*999+m])
        END
    END
END

EOF

